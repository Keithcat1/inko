import std::io::(Error, INVALID_INPUT, NOT_FOUND, Read, Write)
import std::os::NEWLINE
import std::test::*

class Reader {
  static def new -> Self {
    Self {}
  }
}

impl Read for Reader {
  def read_bytes(bytes: ByteArray, size = 0) -> Integer {
    let input = Array.new(105, 110, 107, 111)
    let max = size.positive?.if(true: { size }, false: { input.length })
    let mut index = 0

    while({ index < max }) {
      bytes.push(input[index])
      index += 1
    }

    max
  }
}

class Writer {
  @buffer: Array!(String)

  static def new -> Self {
    Self { @buffer = Array.new }
  }

  def buffer -> Array!(String) {
    @buffer
  }
}

impl Write for Writer {
  def write_bytes(bytes: ByteArray) -> Integer {
    0
  }

  def flush {}

  def write_string(string: String) -> Integer {
    @buffer.push(string)

    string.length
  }
}

def tests(t: Tests) {
  t.group('std::io::Error.new') do (g) {
    g.test('Creating an Error for an error code') {
      let error = Error.new(NOT_FOUND)

      try assert_equal(error.code, NOT_FOUND)
      try assert_equal(error.message, 'The resource could not be found')
    }
  }

  t.group('std::io::Error.invalid_input') do (g) {
    g.test('Creating a custom Error for invalid input') {
      let error = Error.invalid_input('testing')

      try assert_equal(error.code, INVALID_INPUT)
      try assert_equal(error.message, 'testing')
    }
  }

  t.group('std::io::Read.read_string') do (g) {
    g.test('Reading all bytes into a String') {
      let reader = Reader.new
      let string = try! reader.read_string

      try assert_equal(string, 'inko')
    }

    g.test('Reading a limited number of bytes into a String') {
      let reader = Reader.new
      let string = try! reader.read_string(size: 2)

      try assert_equal(string, 'in')
    }
  }

  t.group('std::io::Write.print') do (g) {
    g.test('Printing a String') {
      let writer = Writer.new
      let written = try! writer.print('inko')

      try assert_equal(written, 5)
      try assert_equal(writer.buffer, Array.new('inko', "\n"))
    }
  }
}
