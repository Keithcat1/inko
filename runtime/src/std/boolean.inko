# Boolean true and false.
#
# In Inko boolean true is the object `True` and boolean false is the object
# `False`. Both are regular objects that are instances of the `Boolean` object.
import std::conditional::Conditional
import std::conversion::ToString
import std::hash::(Hasher, Hash)

impl Boolean {
  # Returns the `Boolean` that is the opposite of `self`.
  def not -> Boolean {
    _INKOC.if(self, False, True)
  }
}

impl Conditional for Boolean {
  def if!(R)(true: do -> R, false: do -> R) -> R {
    _INKOC.if(self, true, false).call
  }

  def if_true!(R)(block: do -> R) -> ?R {
    _INKOC.if(self, block.call, Nil)
  }

  def if_false!(R)(block: do -> R) -> ?R {
    _INKOC.if(self, Nil, block.call)
  }

  def and(other: do -> Boolean) -> Boolean {
    _INKOC.if(self, other.call, False)
  }

  def or(other: do -> Boolean) -> Boolean {
    _INKOC.if(self, True, other.call)
  }
}

impl ToString for Boolean {
  def to_string -> String {
    _INKOC.if(self, 'True', 'False')
  }
}

impl Hash for Boolean {
  def hash(hasher: Hasher) -> NilType {
    hasher.write_boolean(self)
    Nil
  }
}
