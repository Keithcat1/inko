#! Boolean true and false.
#!
#! In Inko boolean true is the object `True` and boolean false is the object
#! `False`. Both are regular objects that implement a common trait: `Boolean`.

import std::conditional::Conditional
import std::conversion::ToString
import std::hash::(Hasher, Hash)
import std::operators::Equal

trait Boolean: ToString + Conditional + Hash + Equal {
  ## Returns the `Boolean` that is the opposite of `self`.
  def not -> Boolean
}

impl Conditional for Inko::TrueObject {
  def if_true!(R)(block: do -> R) -> ?R {
    block.call
  }

  def if_false!(R)(block: do -> R) -> ?R {
    Nil
  }

  def if!(R)(true: do -> R, false: do -> R) -> R {
    true.call
  }

  def and(other: do -> Boolean) -> Boolean {
    other.call
  }

  def or(other: do -> Boolean) -> Boolean {
    True
  }
}

impl Conditional for Inko::FalseObject {
  def if_true!(R)(block: do -> R) -> ?R {
    Nil
  }

  def if_false!(R)(block: do -> R) -> ?R {
    block.call
  }

  def if!(R)(true: do -> R, false: do -> R) -> R {
    false.call
  }

  def and(other: do -> Boolean) -> Boolean {
    False
  }

  def or(other: do -> Boolean) -> Boolean {
    other.call
  }
}

impl ToString for Inko::TrueObject {
  def to_string -> String {
    'True'
  }
}

impl ToString for Inko::FalseObject {
  def to_string -> String {
    'False'
  }
}

impl Equal for Inko::TrueObject {
  def ==(other: Self) -> Boolean {
    _INKOC.object_equals(self, other)
  }

  def !=(other: Self) -> Boolean {
    self == other
      .not
  }
}

impl Equal for Inko::FalseObject {
  def ==(other: Self) -> Boolean {
    _INKOC.object_equals(self, other)
  }

  def !=(other: Self) -> Boolean {
    self == other
      .not
  }
}

impl Hash for Inko::TrueObject {
  def hash(hasher: Hasher) {
    _INKOC.hasher_write(hasher, self)
  }
}

impl Hash for Inko::FalseObject {
  def hash(hasher: Hasher) {
    _INKOC.hasher_write(hasher, self)
  }
}

impl Boolean for Inko::TrueObject {
  def not -> Boolean {
    False
  }
}

impl Boolean for Inko::FalseObject {
  def not -> Boolean {
    True
  }
}
