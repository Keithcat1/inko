# Configuration options for a compiler.
import std::env
import std::fs::path::(ToPath, Path)

# The default path to the runtime source files.
let DEFAULT_RUNTIME_PATH = '/usr/lib/inko/runtime'

# The file extension of Inko source files.
let SOURCE_EXTENSION = '.inko'

# The name of the constant used to indicate that sending a message should be
# compiled into a VM instruction.
let INSTRUCTION_RECEIVER_NAME = '_INKOC'

# A type for storing compiler settings, such as the source directories to search
# through.
object Config {
  # The source directories to search through when importing a module.
  @source_directories: Array!(Path)

  static def new -> Self {
    Self { @source_directories = Array.new(default_source_directory) }
  }

  def source_directories -> Array!(Path) {
    @source_directories
  }

  def add_source_directory(path: ToPath) -> Path {
    @source_directories.push(path.to_path)
  }

  def default_source_directory -> Path {
    let from_env = env.get('INKO_RUNTIME_PATH')
    let path = from_env.if(true: { from_env! }, false: { DEFAULT_RUNTIME_PATH })

    Path.new(path)
  }
}
