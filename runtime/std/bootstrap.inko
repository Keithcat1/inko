# Importing of both the prelude and the bootstrap modules will not yet work at
# this point, hence these are disabled.
![import_bootstrap: false]
![import_globals: false]
![import_prelude: false]

# Implicit module definitions will not yet work since we haven't defined the
# required objects just yet.
#
# As a result of this setting all constants and methods defined in this module
# are defined directly on the top-level object.
![define_module: false]

# Set up the various built-in objects that Inko comes with. These objects need
# to be set manually since the "object" keyword can not yet be used at this
# point.
let mut True = _INKOC.get_true
let mut False = _INKOC.get_false
let mut Nil = _INKOC.get_nil

let mut Object = _INKOC.get_object_prototype
let mut Boolean = _INKOC.get_boolean_prototype
let mut String = _INKOC.get_string_prototype
let mut Integer = _INKOC.get_integer_prototype
let mut Float = _INKOC.get_float_prototype
let mut Block = _INKOC.get_block_prototype
let mut Array = _INKOC.get_array_prototype

_INKOC.set_attribute(True, '@_object_name', 'True')
_INKOC.set_attribute(False, '@_object_name', 'False')
_INKOC.set_attribute(Nil, '@_object_name', 'Nil')
_INKOC.set_attribute(Object, '@_object_name', 'Object')
_INKOC.set_attribute(String, '@_object_name', 'String')
_INKOC.set_attribute(Integer, '@_object_name', 'Integer')
_INKOC.set_attribute(Float, '@_object_name', 'Float')
_INKOC.set_attribute(Array, '@_object_name', 'Array')
_INKOC.set_attribute(Block, '@_object_name', 'Block')
_INKOC.set_attribute(Boolean, '@_object_name', 'Boolean')

# Now that our core objects are set up we can start defining more of the
# building blocks of Inko, such as "Object.new" and the bits necessary to allow
# creating of modules.
impl Object {
  def new -> Self {
    let mut obj = self.allocate

    obj.init

    obj
  }

  def allocate -> Self {
    _INKOC.set_object(False, self)
  }

  def init {}
}

# Now that the basics are in place we can define Trait the usual way.
object Trait {}

# Inko::Modules contains all of the imported modules mapped to their fully
# qualified module names.
object Modules {}

# Inkoc::Module will act as the prototype for all modules. We'll later refine
# this object in the std::module module, but we need it here to allow us to
# define new modules in the first place.
object Module {}

# Now we can define the object for the bootstrap module.
_INKOC.set_attribute(Modules, 'std::bootstrap', Module.new)
