import std.drop (drop)
import std.env
import std.fmt (Format, Formatter)
import std.fmt (fmt)
import std.fs.path (Path)
import std.hash (Hash, Hasher)
import std.hash.siphash (SipHasher13)
import std.stdio (STDOUT)

fn pub hash[T: Hash](value: ref T) -> Int {
  let hasher = SipHasher13.default

  value.hash(hasher)
  hasher.finish
}

fn pub with_directory[R](id: Int, func: fn (ref Path) -> R) -> R {
  let path = env.temporary_directory.join('inko-test-directory-${id}')
  let ret = func.call(path)

  path.remove_directory_all
  ret
}

fn pub compiler_path -> Path {
  let base = match env.working_directory {
    case Ok(path) -> path
    case Error(err) -> {
      panic('Failed to determine the working directory: ${err}')
    }
  }

  let target = if base.join('test').directory? {
    base.join('..').join('target')
  } else if base.join('std').directory? {
    base.join('target')
  } else {
    panic('Tests must be run in either the project root, or the std/ directory')
  }

  let debug = target.join('debug').join('inko')
  let release = target.join('release').join('inko')

  match (debug.modified_at, release.modified_at) {
    case (Ok(deb), Ok(rel)) -> if deb >= rel { debug } else { release }
    case (Ok(_), Error(_)) -> debug
    case (Error(_), Ok(_)) -> release
    case _ -> panic("The path to the compiler couldn't be determined")
  }
}
